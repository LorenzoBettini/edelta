modifyEcore introduceWorkingPosition epackage PersonList {
	addNewEClass("WorkingPosition") [
		addNewEAttribute("description", ecoreref(EString))
		extractMetaClass(ecoreref(Person.works), "position", "works")
	]
}

/**
 * @param extractedClass the created EClass created representing the extracted metaclass
 * @param f 
 * @param inReferenceName
 * @param outReferenceName
 */
def extractMetaClass(EClass extractedClass, EReference f, String inReferenceName, String outReferenceName) : void {
	val ref_in = newEReference(inReferenceName, extractedClass) [
		lowerBound = f.EOpposite.lowerBound;
		upperBound = 1;
		// f.EOpposite.upperBound;
	];

	val old_ref = newEReference(f.name, f.EReferenceType) [
		lowerBound = 1;
		upperBound = 1;
		EOpposite = ref_in;
	];

	extractedClass.addEReference(old_ref);
	ref_in.EOpposite = old_ref;
	f.EOpposite.lowerBound = 1;
	f.EOpposite.upperBound = 1;

	extractedClass.addEReference(f.EOpposite);

	// already created from outside in the right package
	// f.EContainingClass.EPackage.EClassifiers+=extracted_class;
	f.EReferenceType.addEReference(ref_in);
	f.EType = extractedClass;
	f.containment = true;
	f.name = outReferenceName;
}

